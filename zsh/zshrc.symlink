# If not running interactively → don't do anything
# [[ $- != *i* ]] && return

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source $file
done

#remember DIRSTACK
DIRSTACKFILE="$HOME/.cache/zsh/dirs"
if [[ -f $DIRSTACKFILE  ]]; then
  if [[ -f $DIRSTACKFILE ]] && [[ $#dirstack -eq 0 ]]; then
    dirstack=( ${(f)"$(< $DIRSTACKFILE)"} )
    [[ -d $dirstack[1] ]] && cd $dirstack[1]
  fi

  chpwd() {
    print -l $PWD ${(u)dirstack} >$DIRSTACKFILE
  }

  DIRSTACKSIZE=20
  setopt AUTO_PUSHD PUSHD_SILENT PUSHD_TO_HOME
  ## Remove duplicate entries
  setopt PUSHD_IGNORE_DUPS
  ## This reverts the +/- operators.
  setopt PUSHD_MINUS
fi

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# history-substring-search
zmodload zsh/terminfo
bindkey "$terminfo[kcuu1]" history-substring-search-up
bindkey "$terminfo[kcud1]" history-substring-search-down
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down

# bind k and j for VI mode
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

if [[ -f /etc/profile.d/autojump.sh ]]; then
  source /etc/profile.d/autojump.sh
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# Stash your environment variables in ~/.zshrc_private. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.zshrc_private ]]; then
  source ~/.zshrc_private
fi

unset config_files

if [[ ! -f $HOME/.zinit/bin/zinit.zsh ]]; then
    print -P "%F{33}▓▒░ %F{220}Installing %F{33}DHARMA%F{220} Initiative Plugin Manager (%F{33}zdharma/zinit%F{220})…%f"
    command mkdir -p "$HOME/.zinit" && command chmod g-rwX "$HOME/.zinit"
    command git clone https://github.com/zdharma/zinit "$HOME/.zinit/bin" && \
        print -P "%F{33}▓▒░ %F{34}Installation successful.%f%b" || \
        print -P "%F{160}▓▒░ The clone has failed.%f%b"
fi
