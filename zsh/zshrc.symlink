# vi: ft=zsh

if [[ ! -f $HOME/.zgenom/zgenom.zsh ]]; then
    print -P "%F{33}▓▒░ %F{220}Installing Initiative Plugin Manager (%F{33}github.com/jandamm/zgenom%F{220})…%f"
    command mkdir -p "$HOME/.zgenom"
    command git clone https://github.com/jandamm/zgenom.git "$HOME/.zgenom" && \
        print -P "%F{33}▓▒░ %F{34}Installation successful.%f%b" || \
        print -P "%F{160}▓▒░ The clone has failed.%f%b"
fi

# If not running interactively → don't do anything
# [[ $- != *i* ]] && return

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source $file
done

# load everything but the path, aliases and completion files
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/aliases.zsh}; do
  source $file
done

# initialize autocomplete here, otherwise functions wont be loaded
autoload -U compinit
compinit

# history-substring-search
zmodload zsh/terminfo
bindkey "$terminfo[kcuu1]" history-substring-search-up
bindkey "$terminfo[kcud1]" history-substring-search-down
bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down

# bind k and j for VI mode
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

if [[ -f /etc/profile.d/autojump.sh ]]; then
  source /etc/profile.d/autojump.sh
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

# load aliases
for file in ${(M)config_files:#*/aliases.zsh}; do
  source $file
done

# Stash your environment variables in ~/.zshrc_private. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.zshrc_private ]]; then
  source ~/.zshrc_private
fi

unset config_files
